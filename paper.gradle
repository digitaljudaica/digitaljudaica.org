ext.configurePaper = {
  final String dataGeneratorClassName,
  final Boolean isMathJaxEnabled
    ->

  final String projectName = project.name
  final String projectVersion = project.version
  final String documentName = (projectName.endsWith('-paper')) ?
    projectName.substring(0, projectName.length() - '-paper'.length()) :
    projectName

  // println("documentName: $documentName")

  docBook {
    document = documentName

    dataGeneratorClass = dataGeneratorClassName

    outputFormats = ["html", "pdf", "epub2"]

    substitutions = [
      "project.version": projectVersion
    ]

    parameters.common = [
      "part.autolabel"       : "0",       // was only in calendar paper
      "chapter.autolabel"    : "0",       // was only in calendar paper
      "section.autolabel.max.depth": "6"  // was only in the non-calendar papers
//      "punct.honorific": "" // Remove trailing period after honorific
    ]

    parameters.htmlCommon = [
      // Include page number in cross-references
      "insert.xref.page.number": "yes"  // was only in the non-calendar papers
    ]

    parameters.pdf = [
      "title.font.family"    : "DejaVu Sans",
      "body.font.family"     : "DejaVu Sans",
      "sans.font.family"     : "DejaVu Sans",
      "monospace.font.family": "DejaVu Sans Mono",
      "symbol.font.family"   : "DejaVu Sans",
      "body.font.master"     : "12",
      "footnote.font.size"   : "10",  // was only in the non-calendar papers

      // Suppress content indent
      "body.start.indent"    : "0pt",  // was only in the non-calendar papers

      // Some space for the list labels
      "orderedlist.label.width": "2em"  // was only in the non-calendar papers
    ]

    mathJax {
      isEnabled = isMathJaxEnabled
    }
  }

  site.dependsOn processDocBook
  site.doLast {
    project.sync {
      from processDocBook.outputDirectory
      into "$rootDir/docs/$documentName/paper"
    }
  }
}
