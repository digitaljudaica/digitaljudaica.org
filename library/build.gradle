plugins {
  id 'scala'
  id 'com.github.maiflai.scalatest'
  id 'com.jfrog.bintray'
  id 'maven-publish'
}

description = 'Jewish Calendar Dates.'

dependencies {
  compile "org.scala-lang:scala-library:$scalaVersion"
  compile "org.scala-lang.modules:scala-xml_$scalaVersionMajor:$scalaXmlVersion"

  testCompile "org.scalacheck:scalacheck_$scalaVersionMajor:$scalacheckVersion"
  testCompile "org.scalatest:scalatest_$scalaVersionMajor:$scalatestVersion"
  testCompile "org.typelevel:discipline-scalatest_$scalaVersionMajor:$disciplineScalatestVersion"

  testRuntime "org.pegdown:pegdown:$pegdownVersion" // for scalatest reports; dead...

  testCompile "org.typelevel:cats-core_$scalaVersionMajor:$catsVersion"
  testCompile "org.typelevel:algebra-laws_$scalaVersionMajor:$algebraLawsVersion"
}

// Do not let 'discipline' drag us into the snapshot releases :)
configurations.all {
  resolutionStrategy {
    force("org.scalatest:scalatest_$scalaVersionMajor:$scalatestVersion")
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  archiveClassifier.set('sources')
}

task scaladocJar(type: Jar) {
  from scaladoc.destinationDir
  archiveClassifier.set('scaladoc')
}
scaladocJar.dependsOn scaladoc

def gitHubRepository    = "jewish-calendar/calendar"
def gitHubRepositoryUrl = "https://github.com/$gitHubRepository"

def libraryArtifactId = "$project.group-$project.name"
jar.archiveBaseName.set(libraryArtifactId)

publishing {
  publications {
    mavenPublication(MavenPublication) {
      groupId project.group
      artifactId libraryArtifactId
      
      from components.java

      artifact sourceJar
      artifact scaladocJar

      pom {
        name = project.name
        description = project.description
        url = "$gitHubRepositoryUrl"
        scm {
          url = "$gitHubRepositoryUrl"
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        organization {
          name = 'Jewish Calendar'
          url = 'http://www.jewish-calendar.org'
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'http://dub.podval.org'
            organization = 'Podval Group'
            organizationUrl = 'http://www.podval.org'
            timezone = '-5'
          }
        }
      }
    }
  }
}

bintray {
  user         = 'dubinsky'
  key          = findProperty('bintrayApiKey')
  publications = ['mavenPublication']
  dryRun       = false
  publish      = true
  override     = true
  pkg {
    repo                   = project.group
    name                   = libraryArtifactId
    desc                   = project.description
    websiteUrl             = "${gitHubRepositoryUrl}"
    issueTrackerUrl        = "${gitHubRepositoryUrl}/issues"
    vcsUrl                 = "${gitHubRepositoryUrl}.git"
    githubRepo             = gitHubRepository
    licenses               = ['Apache-2.0']
    labels                 = ['Scala', 'Calendar', 'Jewish']
    publicDownloadNumbers  = true
    githubReleaseNotesFile = "${rootProject.projectDir}/docs/CHANGELOG.md"
    version { name         = project.version }
  }
}
