plugins {
  id 'scala'
}

// for Tut
repositories {
  maven {
    url 'https://dl.bintray.com/tpolecat/maven/'
  }
}

dependencies {
  compile "org.scala-lang:scala-library:$scalaVersion"

  runtime project(':library')
  runtime "org.tpolecat:tut-core_$scalaVersionMajor:$tutVersion"
}

// Tut
final String tutOutput = "$buildDir/tut"
task tut(type: JavaExec) {
  group = 'documentation'
  main = 'tut.TutMain'
  args = ["$projectDir/tut", tutOutput, ".*\\.(md|markdown|txt|htm|html)"]
  classpath = sourceSets.main.runtimeClasspath
}

build.dependsOn ':paper:processDocBook'
build.dependsOn tut
build.dependsOn ':library:scaladoc'
build.doLast {
  project.sync {
    from project.tasks.getByPath(':paper:processDocBook').outputDirectory
    into 'paper'
  }
  project.sync {
    from tutOutput
    into 'examples'
  }
  project.sync {
    from project.tasks.getByPath(':library:scaladoc').destinationDir
    into 'scaladoc'
  }
}
