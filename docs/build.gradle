// for Tut
repositories {
  maven {
    url 'https://dl.bintray.com/tpolecat/maven/'
  }
}

apply plugin: 'scala'

// for Tut
configurations.all {
  resolutionStrategy {
    force 'org.scala-lang:scala-library:2.12.7'
  }
}

dependencies {
  zinc    "com.typesafe.zinc:zinc:$zincVersion"
  compile "org.scala-lang:scala-library:2.12.7"

  runtime project(':library')
  runtime "org.tpolecat:tut-core_2.12:0.6.10"
}

// Tut
def tutOutput = "$buildDir/tut"
task tut(type: JavaExec) {
  group = 'documentation'
  main = 'tut.TutMain'
  args = ["$projectDir/tut", tutOutput, ".*\\.(md|markdown|txt|htm|html)"]
  classpath = sourceSets.main.runtimeClasspath
}

build.dependsOn tut
build.dependsOn ':library:scaladoc'
build.dependsOn ':paper:processDocBook'
build.doLast {
  project.sync {
    from project.tasks.getByPath(':paper:processDocBook').outputDirectory
    into 'paper'
  }
  project.sync {
    from tutOutput
    into 'examples'
  }
  project.sync {
    from project.tasks.getByPath(':library:scaladoc').destinationDir
    into 'scaladoc'
  }
}
