// for Tut
repositories {
  maven {
    url 'https://dl.bintray.com/tpolecat/maven/'
  }
}

// for Tut
configurations.all {
  resolutionStrategy {
    force 'org.scala-lang:scala-library:2.12.7'
  }
}

dependencies {
  runtime project(':library')
  runtime "org.tpolecat:tut-core_2.12:0.6.10"
}

task tut(type: JavaExec) {
  group = 'documentation'
  main = 'tut.TutMain'
  args = ["$projectDir/tut", "$projectDir/examples", ".*\\.(md|markdown|txt|htm|html)"]
  classpath = sourceSets.main.runtimeClasspath
}

task copyScaladoc(type: Copy) {
  group = 'documentation'
  dependsOn ':library:scaladoc'
  from project(':library').tasks.named('scaladoc').get().destinationDir
  into 'scaladoc'
}

task copyPaper(type: Copy) {
  group = 'documentation'
  dependsOn ':paper:docBookProcess'
  from "${project(':paper').buildDir}/docBook/"
  into 'paper'
}

build.dependsOn tut
//  build.dependsOn copyScaladoc
build.dependsOn copyPaper
