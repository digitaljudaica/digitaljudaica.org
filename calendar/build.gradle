plugins {
  id 'scala'
  id 'com.github.maiflai.scalatest'
  id 'maven-publish'
  id 'com.jfrog.bintray'
}

description = 'Jewish Calendar Dates'
version = calendarProjectVersion

dependencies {
  implementation project(':store')
  implementation project(':xml')
  implementation project(':util')

  zinc "org.scala-sbt:zinc_$scalaVersionMajor:$zincVersion"

  implementation "org.scala-lang:scala-library:$scalaVersion"
  implementation "org.scala-lang.modules:scala-xml_$scalaVersionMajor:$scalaXmlVersion"

  implementation "dev.zio:zio_$scalaVersionMajor:$zioVersion"

  testImplementation "org.typelevel:cats-core_$scalaVersionMajor:$catsVersion"

  runtimeOnly "org.tpolecat:tut-core_$scalaVersionMajor:$tutVersion"

  testImplementation "org.scalatest:scalatest_$scalaVersionMajor:$scalatestVersion"
  testRuntimeOnly "com.vladsch.flexmark:flexmark-all:$flexmarkVersion"  // for scalatest reports; only 0.35.10 is supported?

  testImplementation "org.scalacheck:scalacheck_$scalaVersionMajor:$scalacheckVersion"
  testImplementation "org.typelevel:discipline-scalatest_$scalaVersionMajor:$disciplineScalatestVersion"
  testImplementation "org.typelevel:algebra-laws_$scalaVersionMajor:$algebraLawsVersion"
}

apply from: '../bintray.gradle'
configureBintray(['Scala', 'Calendar', 'Jewish'])

// Tut
final String tutOutputDirectory = "$buildDir/tut"
task tut(type: JavaExec) {
  group = 'documentation'
  main = 'tut.TutMain'
  args = ["$projectDir/src/site", tutOutputDirectory, ".*\\.(md|markdown|txt|htm|html)"]
  classpath = sourceSets.main.runtimeClasspath
}
tut.doLast {
  project.sync {
    from tutOutputDirectory
    into "$rootDir/docs/calendar-examples"
  }
}
