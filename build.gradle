// See http://dub.podval.org/2020/06/29/bintray-gradle-plugin.html for the reasons
// this `buildscript` block is here and Bintray plugin is applied to the root project.
buildscript {
  dependencies {
    classpath ("org.apache.httpcomponents:httpclient") {
      version {
        strictly '4.5.10'
      }
    }
  }
}

plugins {
  id 'com.github.ben-manes.versions'
  id 'idea'
  id 'base'
  id 'com.jfrog.bintray' apply false
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
}

// There is no Java in the project :)
project.gradle.startParameter.excludedTaskNames.add('compileJava')

subprojects {
  group = 'org.opentorah'
  version = projectVersion

  apply from: '../scala.gradle'
  apply from: '../library.gradle'
  apply from: '../mdoc.gradle'
  apply from: '../paper.gradle'

  // TODO there seem to be multiple versions of some dependencies; verify and force as needed:
  // scala-xml, scala-library, slf4j-api, cats-kernel, cats-kernel-laws...
  configurations.all {
    resolutionStrategy {
      // v1.3.04 doesn't have org.w3c.dom.ElementTraversal that Xerces uses:
      force "xml-apis:xml-apis:$xmlApisVersion"
    }
  }

  task site
  site.description('Generate project website')
  site.group('publishing')

  task upload
  upload.description('Upload artifacts and plugins')
  upload.group('publishing')

  task deploy
  deploy.description('Publish containers and re-deploy services')
  deploy.group('publishing')
}

idea {
  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}
