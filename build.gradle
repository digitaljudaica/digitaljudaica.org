import org.gradle.api.tasks.testing.logging.TestLogEvent

// TODO  scalacOptions ++= Seq("-Ypartial-unification") - for http4s

buildscript {
  repositories {
    // To function outside a composite, a plugin repository would be required.
  }

  dependencies {
    classpath 'org.podval.docbook:gradle-docbook-plugin:+'
  }
}

plugins {
  id "com.github.maiflai.scalatest" version "0.22" apply false
  id "com.jfrog.artifactory" version "4.5.4" apply false
  id "com.jfrog.bintray" version "1.7.3" apply false

  // I do not use 'com.bmuschko.docker-java-application' because I want to separate third-party dependencies in a
  // different layer from the org.podval ones.
  //
  // I do not use 'com.bmuschko.docker-remote-api' because I don't need to: Google Cloud Build step takes care
  // of Docker image creation and push; also, Google Cloud Build provides Gradle v4.6, and the plugin requires v4.8.
  //
  // If I didn't use Cloud Build, this is how I'd do it:
  //  task buildDockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: prepareDockerContext) {
  //    group = "docker"
  //    inputDir = dockerDir
  //    tag = "gcr.io/calendar-service-1/service".toString()
  //  }
  //
  //  id 'com.bmuschko.docker-remote-api' version '4.0.5' apply false
  //  id 'com.bmuschko.docker-java-application' version '4.0.5' apply false
}

def scalaVersion      = '2.12.7'
def scalaVersionMajor = '2.12'
def http4sVersion     = '0.18.21' // '0.20.0-SNAPSHOT'

ext.lib = [
  zinc      : 'com.typesafe.zinc:zinc:0.3.15',
//  "org.scala-sbt:zinc_${scalaVersionMajor}:1.0.3"
  scala          : "org.scala-lang:scala-library:$scalaVersion",
  scalaXml       : "org.scala-lang.modules:scala-xml_$scalaVersionMajor:1.1.0",

  slf4jJdk       : "org.slf4j:slf4j-jdk14:1.7.25",

  http4sDsl      : "org.http4s:http4s-dsl_$scalaVersionMajor:$http4sVersion",
  http4sScalatags: "org.http4s:http4s-scalatags_$scalaVersionMajor:$http4sVersion",
  http4sServer   : "org.http4s:http4s-blaze-server_$scalaVersionMajor:$http4sVersion",
  http4sClient   : "org.http4s:http4s-blaze-client_$scalaVersionMajor:$http4sVersion",

  scalatags      : "com.lihaoyi:scalatags_$scalaVersionMajor:0.6.7",

  scalaCheck     : "org.scalacheck:scalacheck_${scalaVersionMajor}:1.14.0",
  scalaTest      : "org.scalatest:scalatest_${scalaVersionMajor}:3.0.5",
  pegdown        : 'org.pegdown:pegdown:1.4.2' // for scalatest reports
]

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }

  group = 'org.podval.calendar'
  version = '0.1.1'
}

subprojects {
  apply plugin: 'scala'
  apply plugin: 'com.github.maiflai.scalatest'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'

  jar.baseName = "$project.group-$project.name"

  dependencies {
    zinc lib.zinc
    compile lib.scala
    testImplementation lib.scalaCheck, lib.scalaTest, lib.pegdown
  }

  test {
    testLogging {
      events = [TestLogEvent.FAILED]
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
  }
}

apply plugin: 'idea'

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}


def gitHubRepository    = "jewish-calendar/calendar"
def gitHubRepositoryUrl = "https://github.com/${gitHubRepository}"

def artifactUrls = [
  siteUrl  : "${gitHubRepositoryUrl}",
  issuesUrl: "${gitHubRepositoryUrl}/issues",
  vcsUrl   : "${gitHubRepositoryUrl}.git"
]

def pomConfig = {
  url artifactUrls.siteUrl
  scm { url artifactUrls.siteUrl }
  licenses {
    license {
      name 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
      comments 'A business-friendly OSS license'
    }
  }
  organization {
    name 'Podval Group'
    url 'http://www.podval.org'
  }
  developers {
    developer {
      id 'dub'
      name 'Leonid Dubinsky'
      email 'dub@podval.org'
      url 'http://dub.podval.org'
      organization 'Podval Group'
      organizationUrl 'http://www.podval.org'
      roles { role 'developer' }
      timezone '-5'
    }
  }
}

def bintrayInfo = [
  user: 'dubinsky',
  password: findProperty('bintrayApiKey'),
  repository: 'org.podval'
]

def bintrayConfig = {
  user         = bintrayInfo.user
  key          = bintrayInfo.password
  publications = ['mavenPublication']
  dryRun       = false
  publish      = true
  override     = false
  pkg {
    repo                   = bintrayInfo.repository
    name                   = project.group
    desc                   = 'Jewish Calendar'
    websiteUrl             = artifactUrls.siteUrl
    issueTrackerUrl        = artifactUrls.issuesUrl
    vcsUrl                 = artifactUrls.vcsUrl
    githubRepo             = gitHubRepository
    licenses               = ['Apache-2.0']
    labels                 = ['Scala', 'Calendar', 'Jewish']
    publicDownloadNumbers  = true
    githubReleaseNotesFile = 'README.md'
    version { name         = project.version }
  }
}

project(':judaica') {
  dependencies {
    compile lib.scalaXml
  }
}

project(':dates') {
  dependencies {
    compile project(':judaica')
  }

  def artifactInfo = [
    id: 'org.podval.calendar.dates',
    name: 'jewish-calendar-dates',
    description: 'Jewish Calendar Dates.'
  ]

  publishing {
    publications {
      mavenPublication(MavenPublication) {
        artifactId artifactInfo.id

        from components.java
        artifact sourceJar

        pom.withXml {
          def root = asNode()
          root.appendNode('description', artifactInfo.description)
          root.appendNode('name'       , artifactInfo.name       )
          root.children().last() + pomConfig
        }
      }
    }
  }

  bintray bintrayConfig
}

project(':service') {
  apply plugin: 'application'

  dependencies {
    compile project(':dates')
    compile lib.http4sDsl, lib.http4sServer, lib.http4sClient
    compile lib.scalatags
    // http4s -> Scalatags bridge, when included as a dependency, confuses the imports;
    // doing explicit conversions for now :)    
    //    compile lib.http4sScalatags
    implementation lib.slf4jJdk
  }

  mainClassName = 'org.podval.calendar.service.CalendarService'

  def dockerDir = file("$buildDir/docker/")

  task prepareDockerContext {
    group = "docker"
    dependsOn installDist

    doLast {
      copy { from "$installDist.destinationDir/lib" into "$dockerDir/project-libs" include "**/org.podval.*" }
      copy { from "$installDist.destinationDir/lib" into "$dockerDir/dependencies" exclude "**/org.podval.*" }
      copy { from "$installDist.destinationDir/bin" into "$dockerDir/bin" }
      copy { from "$projectDir/Dockerfile" into dockerDir }
    }
  }
}

project(':paper') {
  apply plugin: 'org.podval.docbook.gradle'

  dependencies {
    compile lib.scalaXml, project(':dates')
  }

  def docBookData = file("${buildDir}/data")

  docbook {
    inputFileName = "calendar"

    dataDirectory = docBookData

    xslParameters = [
      "title.font.family": "Noto Sans, Noto Sans Hebrew",
      "body.font.family": "Noto Sans, Noto Sans Hebrew",
//      "dingbat.font.family": "???", // The font family for copyright, quotes, and other symbols
//      "sans.font.family": "???",
//      "monospace.font.family": "monospace",
//      "symbol.font.family": "???" // TODO configure with catch-all symbol font(s)
      "body.font.master": "12"
//      "footnote.font.size": "10"
    ]

    // TODO define entities:
    // version: project.version.toString
    // tables-directory: ${buildDir}/tables
    // date: LocalDate.now().format(DateTimeFormatter.ofPattern("d MMM yyyy"))
  }

  task buildTables(type: JavaExec, dependsOn: [classes]) {
    classpath = sourceSets.main.runtimeClasspath
    main = "org.podval.calendar.paper.Tables"
    args = [ "$docBookData" ]
  }
  prepareDocBookData.dependsOn.add(buildTables)

  docBookHtml.doLast {
    def outputDirectory = file(docBookHtml.outputDirectory)
    copy { from "src/main/images" into "$outputDirectory/images" }
    copy { from "src/main/js"     into "$outputDirectory/js"     }
  }
}
