plugins {
  id "com.github.maiflai.scalatest" version "0.23" apply false
  id "com.gradle.plugin-publish" version "0.10.1" apply false
  id "com.jfrog.bintray" version "1.8.4" apply false
}

group = 'org.podval.docbook'
version = '0.5.0'
description = 'Gradle DocBook plugin.'

repositories {
  jcenter()
}

apply plugin: 'scala'

def scalaVersion = '2.12.8'
def scalaVersionMajor = '2.12'
def fopVersion = '2.3'

// v1.3.04 doesn't have org.w3c.dom.ElementTraversal that Xerces uses:
configurations.all {
  resolutionStrategy {
    force "xml-apis:xml-apis:1.4.01"
  }
}

dependencies {
  // Zinc
  zinc 'com.typesafe.zinc:zinc:0.3.15'

  // Scala
  compile "org.scala-lang:scala-library:$scalaVersion"

  // XMLResolver
  compile 'org.xmlresolver:xmlresolver:0.14.0'

  // Saxon
  compile 'saxon:saxon:6.5.3'

  // Saxon 9
  compile 'net.sf.saxon:Saxon-HE:9.8.0-14'

  // Xerces
  runtime 'xerces:xercesImpl:2.11.0' // TODO 2.12.0?

  // FOP
  compile "org.apache.xmlgraphics:fop:$fopVersion"

  // JEuclid
  compile 'de.rototor.jeuclid:jeuclid-core:3.1.13'
  // old:  compile 'net.sourceforge.jeuclid:jeuclid-fop:3.1.9'

  testImplementation "org.scalatest:scalatest_$scalaVersionMajor:3.0.5"
// added by the 'java-gradle-plugin': testImplementation gradleTestKit()
}

// TODO do I even need this - or does 'java-gradle-plugin' take care of it?
// TODO do I need to add ScalaDoc jar (JavaDoc one is built by the publishPluginJavaDocsJar task)?
task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  // TODO what non-deprecated way is there?
  classifier = 'sources'
}

apply plugin: 'com.github.maiflai.scalatest'

apply plugin: 'java-gradle-plugin'

// TODO what non-deprecated way is there?
// TODO is there a way to affect pluginArtifactId used by publishPlugin[JavaDocs]Jar tasks
//  if it is *not* the project name?
def pluginArtifactId = jar.baseName

gradlePlugin {
  plugins {
    docBookPlugin {
      id = pluginArtifactId
      implementationClass = 'org.podval.docbook.gradle.DocBookPlugin'
    }
  }
}

apply plugin: 'com.gradle.plugin-publish'

def gitHubRepository    = "dubinsky/podval-docbook-gradle"
def gitHubRepositoryUrl = "https://github.com/$gitHubRepository"
def pluginTags = ['Scala', 'DocBook', 'Gradle', 'Plugin', "Html", "Pdf", "Epub", "MathML", "JEuclid"]

pluginBundle {
  website = "${gitHubRepositoryUrl}"
  vcsUrl = "${gitHubRepositoryUrl}.git"

  plugins {
    docBookPlugin {
      // id and displayName are mandatory;
      // id is captured from java-gradle-plugin configuration.
      // TODO displayName and description can be set in the gradlePlugin closure!
      displayName = project.description
      description = project.description
      tags        = pluginTags
      version     = project.version  // defaults to the project version
    }
  }

  // If you have an existing plugin deployed to Bintray and would like to keep
  // your existing group ID and artifact ID for continuity, you can specify
  // them here (needs manual approval on first publication).
  mavenCoordinates {
    groupId    = project.group
    artifactId = pluginArtifactId
    version    = project.version // defaults to the project version
  }
}

apply plugin: 'maven-publish'

publishing {
  publications {
    mavenPublication(MavenPublication) {
      groupId project.group
      artifactId pluginArtifactId

      from components.java
      artifact sourceJar

      pom {
        name = project.name
        description = project.description
        url = "$gitHubRepositoryUrl"
        scm {
          url = "$gitHubRepositoryUrl"
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'http://dub.podval.org'
            organization = 'Podval Group'
            organizationUrl = 'http://www.podval.org'
            timezone = '-5'
          }
        }
      }
    }
  }
}

apply plugin: 'com.jfrog.bintray'

bintray {
  user         = 'dubinsky'
  key          = findProperty('bintrayApiKey')
  publications = ['mavenPublication']
  dryRun       = false
  publish      = true
  override     = true
  pkg {
    repo                   = 'org.podval.docbook'
    name                   = pluginArtifactId
    desc                   = project.description
    websiteUrl             = "${gitHubRepositoryUrl}"
    issueTrackerUrl        = "${gitHubRepositoryUrl}/issues"
    vcsUrl                 = "${gitHubRepositoryUrl}.git"
    githubRepo             = gitHubRepository
    licenses               = ['Apache-2.0']
    labels                 = pluginTags
    publicDownloadNumbers  = true
    githubReleaseNotesFile = 'CHANGELOG.md'
    version { name         = project.version }
  }
}

apply plugin: 'idea'

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}
