// If I annotate the test classes to use ScalaTest's JUnit runner, Idea doesn't give me an option
// to run/debug individual test methods (in addition to this annotation noise);
// if I use ScalaTest Gradle plugin, there is no way (currently) to get rid of the all
// build-time test-related noise :(
plugins {
  id "com.github.maiflai.scalatest" version "0.18" apply false
}

import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: 'idea'

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}

//jar.baseName = 'org.podval.calendar.dates'

allprojects {
  repositories {
    jcenter()
  }

  group = 'org.podval.calendar'
  version = '1.0-SNAPSHOT'
}

def scalaVersion      = '2.12.4'
def scalaVersionMajor = '2.12'

ext.lib = [
  zinc      :
  'com.typesafe.zinc:zinc:0.3.15',
//    'org.scala-sbt:zinc:1.0',
  scala     : "org.scala-lang:scala-library:$scalaVersion",
  scalaTest : "org.scalatest:scalatest_${scalaVersionMajor}:3.0.4",
  pegdown   : 'org.pegdown:pegdown:1.4.2', // for scalatest reports
  scalaCheck: "org.scalacheck:scalacheck_${scalaVersionMajor}:1.13.4"
]

// TODO add GIT scm link
def pomConfig = {
  description 'Jewish Calendar Dates.'
  licenses {
    license {
      name = 'The Apache Software License, Version 2.0'
      url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      distribution 'repo'
      comments 'A business-friendly OSS license'
    }
  }
  organization {
    name 'Podval Group'
    url 'http://www.podval.org'  // http://www.jewish-calendar.org/
  }
  developers {
    developer {
      id 'dub'
      name 'Leonid Dubinsky'
      email 'dub@podval.org'
      url 'http:dub.podval.org'
      organization 'Podval Group'
      organizationUrl 'http://www.podval.org'
      roles {
        role 'developer'
      }
      timezone '-5'
    }
  }
}

project(':dates') {
  apply plugin: 'scala'
  apply plugin: 'com.github.maiflai.scalatest'
  apply plugin: 'maven'

  dependencies {
    zinc lib.zinc
    compile lib.scala
    testImplementation lib.scalaTest, lib.scalaCheck
    testRuntime lib.pegdown
  }

  test {
    testLogging {
      test.testLogging.events = [TestLogEvent.FAILED]
    }
  }

  install {
    repositories.mavenInstaller {
      pom.project pomConfig
    }
  }

  uploadArchives {
    repositories.mavenDeployer {
//    repository(...)
      pom.project pomConfig
    }
  }
}

project(':astronomy') {
  apply plugin: 'scala'
  apply plugin: 'com.github.maiflai.scalatest'

  dependencies {
    zinc lib.zinc
    compile lib.scala, project(':dates')
    testImplementation lib.scalaTest
    testRuntime lib.pegdown
  }

  test {
    testLogging {
      test.testLogging.events = [TestLogEvent.FAILED]
    }
  }
}

project(':generate') {
  apply plugin: 'scala'
  apply plugin: 'com.github.maiflai.scalatest'

  dependencies {
    zinc lib.zinc
    compile lib.scala, project(':dates')
    testImplementation lib.scalaTest
    testRuntime lib.pegdown
  }

  test {
    testLogging {
      test.testLogging.events = [TestLogEvent.FAILED]
    }
  }
}
