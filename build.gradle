plugins {
  id "com.jfrog.bintray" version "1.8.4" apply false
}

repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
}

group = 'org.podval.docbook'
version = '0.1.0'

description = 'Gradle DocBook plugin.'

apply plugin: 'scala'

def scalaVersion = '2.12.8'
def scalaVersionMajor = '2.12'

configurations {
  docBookXslConfiguration
}

dependencies {
  zinc 'com.typesafe.zinc:zinc:0.3.15'
  compile "org.scala-lang:scala-library:$scalaVersion"

  compile 'saxon:saxon:6.5.3'
  runtime 'xerces:xercesImpl:2.11.0'

  compile 'org.apache.xmlgraphics:fop:2.3'

  testCompile "org.scalatest:scalatest_$scalaVersionMajor:3.0.5"

  docBookXslConfiguration "net.sf.docbook:docbook-xsl:1.79.1:resources@zip"
}

task ('prepareDocBookXsl', type: Copy) {
  from(project.zipTree(configurations.getByName('docBookXslConfiguration').singleFile))
  into("$buildDir/docBookXsl")
}

processTestResources.dependsOn(prepareDocBookXsl)

task sourceJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

apply plugin: 'java-gradle-plugin'

gradlePlugin {
  plugins {
    customPlugin {
      id = 'org.podval.docbook-gradle-plugin'
      implementationClass = 'org.podval.docbook.gradle.DocBookPlugin'
    }
  }
}

apply plugin: 'maven-publish'

def gitHubRepository    = "dubinsky/podval-docbook-gradle"
def gitHubRepositoryUrl = "https://github.com/$gitHubRepository"

publishing {
  publications {
    mavenPublication(MavenPublication) {
      groupId project.group
      artifactId jar.baseName

      from components.java
      artifact sourceJar

      pom {
        name = project.name
        description = project.description
        url = "$gitHubRepositoryUrl"
        scm {
          url = "$gitHubRepositoryUrl"
          connection = "scm:git:git://github.com/${gitHubRepository}.git"
          developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
            comments = 'A business-friendly OSS license'
          }
        }
        developers {
          developer {
            id = 'dub'
            name = 'Leonid Dubinsky'
            email = 'dub@podval.org'
            url = 'http://dub.podval.org'
            organization = 'Podval Group'
            organizationUrl = 'http://www.podval.org'
            timezone = '-5'
          }
        }
      }
    }
  }
}

apply plugin: 'com.jfrog.bintray'

bintray {
  user         = 'dubinsky'
  key          = findProperty('bintrayApiKey')
  publications = ['mavenPublication']
  dryRun       = false
  publish      = true
  override     = true
  pkg {
    repo                   = 'org.podval.docbook'
    name                   = project.jar.baseName
    desc                   = project.description
    websiteUrl             = "${gitHubRepositoryUrl}"
    issueTrackerUrl        = "${gitHubRepositoryUrl}/issues"
    vcsUrl                 = "${gitHubRepositoryUrl}.git"
    githubRepo             = gitHubRepository
    licenses               = ['Apache-2.0']
    labels                 = ['Scala', 'Gradle', 'DocBook']
    publicDownloadNumbers  = true
    githubReleaseNotesFile = 'CHANGELOG.md'
    version { name         = project.version }
  }
}


apply plugin: 'idea'

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}
