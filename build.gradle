plugins {
  id 'com.github.ben-manes.versions'
  id 'idea'
  id 'base'
  // Note: Bintray plugin applied to a module reaches into other modules,
  // which in some cases results in the following exception
  //   Cannot cast object ... with class ... to class ...
  // for some class within the Bintray plugin
  // (class-loader issue; see http://forums.jfrog.org/Gradle-plugin-and-multi-projects-td7580521.html).
  // For instance, this happens when:
  // - `bintrayUpload` task for a specific module is run;
  // - a module has both Bintray and Gradle Plugin Portal publishing plugin.
  // It doesn't seem to be likely that Bintray will ever fix the problem;
  // the workaround is: add Bintray plugin on the top level of the project *in addition* to the modules
  // that actually use it, but mark it as not applied (there is no Bintray configuration on the top level,
  // so plugin will fail with an NPE if applied):
  id 'com.jfrog.bintray' apply false
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }

  group = 'org.opentorah'
  version = projectVersion
}

subprojects {
  apply from: '../scala.gradle'
  apply from: '../bintray.gradle'
  apply from: '../mdoc.gradle'

  task site
  site.group('publishing')

  configurations.all {
    resolutionStrategy {
      // v1.3.04 doesn't have org.w3c.dom.ElementTraversal that Xerces uses:
      force "xml-apis:xml-apis:$xmlApisVersion"
    }
  }
}

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}
