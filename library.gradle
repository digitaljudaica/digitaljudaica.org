ext.configureLibrary = {
  final List<String> tags,
  final boolean addSlf4jImplementationForTests = true
    ->

  configureScala(addSlf4jImplementationForTests)
  configureMdoc()

  final String projectName = project.name
  final String projectDescription = project.description
  final String projectGroup = project.group
  final String projectArtifact = "opentorah-$projectName"
  final String projectVersion = project.version
  final String gitHubRepository = "opentorah/opentorah"
  final String gitHubRepositoryUrl = "https://github.com/$gitHubRepository"
  final String codeUrl = "$gitHubRepositoryUrl/tree/master/$projectName"
  final String orgName = 'Open Torah Project'
  final String orgUrl = 'http://www.opentorah.org'

  final String bintrayApiKey = findProperty('bintrayApiKey') ?: System.getenv('bintrayApiKey')
  if (bintrayApiKey == null) project.logger.error("No bintrayApiKey!")

  // Because why not?
  jar {
    manifest {
      attributes(
        'Implementation-Title'  : projectDescription,
        'Implementation-Version': projectVersion
      )
    }
  }

  jar.archiveBaseName.set(projectArtifact)

  // Gradle plugin [publishing?] plugin, if it is applied, adds tasks publishPluginJar and
  // publishPluginJavaDocsJar that create sources and javadoc archives;
  // attempts to use them as artifacts in the Maven publication failed,
  // so I make my own, replacing javadoc with ScalaDoc :)

  task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
  }

  task scaladocJar(type: Jar) {
    from scaladoc.destinationDir
    archiveClassifier.set('scaladoc')
  }
  scaladocJar.dependsOn scaladoc

  publishing {
    publications {
      bintrayMavenPublication(MavenPublication) {
        groupId projectGroup
        artifactId projectArtifact
        version projectVersion

        from components.java

        artifact sourceJar
        // TODO didn't work for Scala 2.13 in Gradle 6.0...
        ///// artifact scaladocJar

        pom {
          name = projectName
          description = projectDescription
          url = codeUrl
          scm {
            url = "$gitHubRepositoryUrl"
            connection = "scm:git:git://github.com/${gitHubRepository}.git"
            developerConnection = "scm:git:ssh://github.com/${gitHubRepository}.git"
          }
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
              comments = 'A business-friendly OSS license'
            }
          }
          organization {
            name = orgName
            url = orgUrl
          }
          developers {
            developer {
              id = 'dub'
              name = 'Leonid Dubinsky'
              email = 'dub@opentorah.org'
              organization = orgName
              organizationUrl = orgUrl
              timezone = '-5'
            }
          }
        }
      }
    }
  }

  bintray {
    user         = 'dubinsky'
    key          = bintrayApiKey
    publications = ['bintrayMavenPublication']
    dryRun       = false
    publish      = true
    override     = true
    pkg {
      repo                  = projectGroup
      name                  = projectArtifact
      desc                  = projectDescription
      websiteUrl            = codeUrl
      issueTrackerUrl       = "${gitHubRepositoryUrl}/issues"
      vcsUrl                = "${gitHubRepositoryUrl}.git"
      githubRepo            = gitHubRepository
      licenses              = ['Apache-2.0']
      labels                = tags
      publicDownloadNumbers = true
      // Note: there seems to be no way to supply per-module CHANGELOG.md -
      // and package creation fails unless there is an overall one...
      githubReleaseNotesFile= 'CHANGELOG.md'
      version { name        = projectVersion }
    }
  }

  upload.dependsOn(bintrayUpload)
}
