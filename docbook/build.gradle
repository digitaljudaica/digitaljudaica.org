repositories {
  jcenter()
  mavenCentral()
  mavenLocal()
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'scala'

group = 'org.podval.docbook'
version = '0.1.0'

gradlePlugin {
  plugins {
    customPlugin {
      id = 'org.podval.docbook.gradle'
      implementationClass = 'org.podval.docbook.gradle.DocBookPlugin'
    }
  }
}


def scalaVersion      = '2.12.5'
def scalaVersionMajor = '2.12'

ext.lib = [
  zinc           : 'com.typesafe.zinc:zinc:0.3.15',
  scala          : "org.scala-lang:scala-library:$scalaVersion",

  saxon          : 'saxon:saxon:6.5.3',
  xerces         : 'xerces:xercesImpl:2.11.0',

  fop            : 'org.apache.xmlgraphics:fop:2.2@jar',
  xmlgraphics    : 'org.apache.xmlgraphics:xmlgraphics-commons:2.2',
  batikTranscoder: 'org.apache.xmlgraphics:batik-transcoder:1.9',
  avalon         : 'org.apache.avalon.framework:avalon-framework-impl:4.3.1',

  // v1.79.2 has been out for a while, but doesn't seem to have made it into Mavrn repositories...
  docbookxslt    : 'net.sf.docbook:docbook-xsl:1.79.1:resources@zip'

//        xslthl: 'net.sf.xslthl:xslthl:2.1.0'
]


configurations {
  docbookxslt
}

dependencies {
  zinc lib.zinc
  compile lib.scala

  compile gradleApi()

  // DocBook XSLT stylesheets are in XSLT 1.0; rewrite in XSLT 2.0 did not yet release anything.
  //
  // On http://saxon.sourceforge.net/, it says:
  //    "even if your stylesheets only require XSLT 1.0, it is probably better to run them under Saxon 9.x"
  //
  // Saxon-HE had support for XSLT 1.0 up to v9.7, then dropped it, then - allegedly - reinstated it
  // by popular demand in v9.8.0.7. It doesn't seem fixed to me; when I use v9.8.0-10, I see a lot of
  //   "Static error... XPST0008: Variable... has not been declared (or its declaration is not in scope),
  // but when I use v9.7.0-21 - I do not.
  //
  // But it still doesn't work; I get (from DocBook XSLT):
  //   Don't know how to chunk with Saxonica
  //   Error at char 16 in xsl:value-of/@select on line 84 column 63 of chunker.xsl:
  //   XTMM9000: Processing terminated by xsl:message at line 84 in chunker.xsl
  //
  // It seems that the extensions that DocBook XSLT stylesheets used to produce multiple output files are
  // not supported even by Saxon-HE versions that supports XSLT 1.0.
  //
  // I had to downgrade to a version of Saxon that is an XSLT 1.0 processor (with the needed extensions) :(
  // That seems to be v6.5.5, but it is not available from Maven Central or JCenter, so I had to go with v6.5.3 :(
  compile lib.saxon
  runtime lib.xerces

  compile lib.fop
  compile lib.batikTranscoder
  runtime lib.avalon

  docbookxslt lib.docbookxslt
}


apply plugin: 'idea'

idea {
  project {
    jdkName = '1.8'
    vcs = 'Git'
  }

  module {
    excludeDirs += [
      file('.idea'),
      file('gradle')
    ]
  }
}
