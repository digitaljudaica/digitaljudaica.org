plugins {
  id 'scala'
  id 'com.github.maiflai.scalatest'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.jfrog.bintray'
  id 'com.gradle.plugin-publish'
}

version = docBookProjectVersion
description = 'Gradle DocBook Plugin'

dependencies {
  implementation project(':fop')
  implementation project(':util')

  // Scala
  zinc "org.scala-sbt:zinc_$scalaVersionMajor:$zincVersion"
  implementation "org.scala-lang:scala-library:$scalaVersion"

  // Testing
  testImplementation "org.scalatest:scalatest_$scalaVersionMajor:$scalatestVersion"
  testRuntimeOnly "com.vladsch.flexmark:flexmark-all:$flexmarkVersion"  // for scalatest reports

  // added by the 'java-gradle-plugin': testImplementation gradleTestKit()
}

// run tests sequentially
test {
  maxParallelForks = 1
}

jar {
  manifest {
    attributes(
      'Implementation-Title'  : project.description,
      'Implementation-Version': project.version
    )
  }
}

final List<String> projectTags = ['Scala', 'DocBook', 'MathML', 'MathJax', 'JEuclid', 'Html', 'Pdf', 'Epub']

configureBintray(projectTags)

gradlePlugin {
  plugins {
    docBookPlugin {
      id = 'org.opentorah.docbook'
      implementationClass = 'org.opentorah.docbook.plugin.DocBookPlugin'
    }
  }
}

pluginBundle {
  website = 'https://github.com/opentorah/opentorah/tree/master/docbook'
  vcsUrl  = 'https://github.com/opentorah/opentorah/tree/master/docbook' //.git' ?

  plugins {
    docBookPlugin {
      displayName = project.description
      description = project.description
      tags        = projectTags
    }
  }

  // If you have an existing plugin deployed to Bintray and would like to keep
  // your existing group ID and artifact ID for continuity, you can specify
  // them here (needs manual approval on first publication).
  mavenCoordinates {
    groupId    = 'org.opentorah'
    artifactId = 'opentorah-docbook'
  }
}
