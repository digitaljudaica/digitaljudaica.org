ext.configureMdoc = {
  final String projectName = project.name

  // To run mdoc I have to have it on JavaExec task's classpath, but I can't just put it on
  // runtimeOnly classpath, because it brings in version of FlexMark that breaks ScalaTest
  // (that only works with a specific, much earlier version).
  // So I put mdoc and its dependencies into a separate configuration
  // and combine it with the main runtime classpath when executing mdoc.
  configurations {
    mdoc
  }

  dependencies {
    mdoc "org.scalameta:mdoc_$scalaVersionMajor:$mdocVersion"
  }

  final String outputDirectory = "$buildDir/docs/mdoc"
  task mdoc(type: JavaExec) {
    group = 'documentation'
    main = 'mdoc.Main'
    args = [
      // '--verbose',
      '--in', "$projectDir/src/docs",
      '--out', outputDirectory
      // default is 'md' and 'html' - and I do not know how to set a set of them...
      //, '--markdown-extensions', 'md,html'
    ]
    classpath = (sourceSets.main.runtimeClasspath + configurations.mdoc)
  }
  mdoc.doLast {
    project.sync {
      from outputDirectory
      into "$rootDir/docs/docs/$projectName"
    }
  }
}
