ext.configureScala = {
  dependencies {
    // zinc
    //   first version of zinc that ships for Scala 2.13 is 1.4.0, but doesn't seem to work for me...
    //   previous version - 1.3.5 - doesn't work with Scala 2.13...
    //   on the other hand, build works without any
    //     zinc "org.scala-sbt:zinc_$scalaVersionMajor:$zincVersion"
    //   just fine ;)

    implementation "org.scala-lang:scala-library:$scalaVersion"
//    implementation "org.scala-lang:scala3-library_$scalaVersionMajor:$scalaVersion"

    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "org.scalatest:scalatest_$scalaVersionMajor:$scalatestVersion"
    // for scalatest reports
    testRuntimeOnly "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    testRuntimeOnly "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"

    testImplementation "org.scalacheck:scalacheck_$scalaVersionMajor:$scalacheckVersion"
    testImplementation "org.typelevel:discipline-scalatest_$scalaVersionMajor:$disciplineScalatestVersion"
    testImplementation "org.typelevel:algebra-laws_$scalaVersionMajor:$algebraLawsVersion"
  }

  // TODO move into the main build file
  tasks.withType(ScalaCompile) {
    scalaCompileOptions.with {
      targetCompatibility = '1.8'
      additionalParameters = ['-feature']
    }
  }
}

ext.configureService = {
  configureScala()

  dependencies {
    api            "dev.zio:zio_$scalaVersionMajor:$zioVersion"
    implementation "dev.zio:zio-interop-cats_$scalaVersionMajor:$zioInteropCatsVersion"
    implementation "org.typelevel:cats-core_$scalaVersionMajor:$catsVersion"
    implementation "org.typelevel:cats-effect_$scalaVersionMajor:$catsEffectVersion"

    implementation "org.http4s:http4s-dsl_$scalaVersionMajor:$http4sVersion"
    implementation "org.http4s:http4s-blaze-server_$scalaVersionMajor:$http4sVersion"
    implementation "org.http4s:http4s-blaze-client_$scalaVersionMajor:$http4sVersion"

    runtimeOnly    "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstashVersion"
  }
}
