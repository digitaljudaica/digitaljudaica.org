plugins {
  id 'scala'
  id 'application'
}

description = 'Jewish Calendar Service.'

dependencies {
  implementation project(':library')
  implementation "org.digitaljudaica:org.digitaljudaica-store:$storeVersion"

  implementation "org.http4s:http4s-dsl_$scalaVersionMajor:$http4sVersion"
  implementation "org.http4s:http4s-blaze-server_$scalaVersionMajor:$http4sVersion"
  implementation "org.http4s:http4s-blaze-client_$scalaVersionMajor:$http4sVersion"

  implementation "com.lihaoyi:scalatags_$scalaVersionMajor:$scalatagsVersion"

  runtimeOnly "org.slf4j:slf4j-jdk14:$slf4jVersion"
}

mainClassName = 'org.podval.calendar.service.CalendarService'

final File dockerDir = file("$buildDir/docker/")

task prepareDockerContext {
  group = "docker"
  dependsOn installDist

  doLast {
    copy { from "$installDist.destinationDir/lib" into "$dockerDir/project-libs" include "**/org.podval.*" }
    copy { from "$installDist.destinationDir/lib" into "$dockerDir/dependencies" exclude "**/org.podval.*" }
    copy { from "$installDist.destinationDir/bin" into "$dockerDir/bin" }
    copy { from "$projectDir/Dockerfile" into dockerDir }
  }
}
