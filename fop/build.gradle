plugins {
  id 'scala'
  id 'com.github.maiflai.scalatest'
  id 'maven-publish'
  id 'com.jfrog.bintray'
}

version = docBookProjectVersion
description = 'FOP MathJax plugin'

configureLibrary(['Scala', 'MathML', 'MathJax', 'JEuclid', 'FOP', 'Pdf'])

dependencies {
  implementation project(':util')

  // XMLResolver
  implementation "org.xmlresolver:xmlresolver:$xmlresolverVersion"

  // Saxon 6 (XSLT 1.0)
  implementation "saxon:saxon:$saxon6Version"

  // Saxon 9 (XSLT 2.0)
  implementation "net.sf.saxon:Saxon-HE:$saxon9Version"

  // Xerces
  implementation "xerces:xercesImpl:$xercesVersion"

  // FOP
  // FOP 2.4 uses JAI and JAI Codec from JBoss repository;
  // since at plugin application time it is to late to define the repository *or* to substitute JAI modules with the
  // ones from JCenter, and I do not want every project that uses the plugin to have to do either,
  // I exclude them and add the ones from JCenter;
  // I have to also exclude FOP from the JEuclid dependencies lest it drags the JAI back in...
  implementation ("org.apache.xmlgraphics:fop:$fopVersion") {
    exclude group: 'javax.media', module: 'jai-core'
    exclude group: 'com.sun.media', module: 'jai-codec'
  }

  implementation "javax.media.jai:com.springsource.javax.media.jai.core:$jaiVersion"
  implementation "javax.media.jai:com.springsource.javax.media.jai.codec:$jaiVersion"

  // JEuclid
  implementation ("de.rototor.jeuclid:jeuclid-fop:$jeuclidFopVersion") {
    exclude group: 'org.apache.xmlgraphics', module: 'fop'
  }

  implementation "org.apache.xmlgraphics:batik-transcoder:$batikVersion"
  implementation "org.apache.xmlgraphics:batik-extension:$batikVersion"

  // Hyphenation patterns
  implementation "net.sf.offo:fop-hyph:$fopHyphVersion"

  // J2V8 integration
  // Note: Native library (if needed and available) is loaded from a dynamically retrieved artifact,
  // not from this one - but it needs to be compatible with the Java JNI wrappers, so I probably
  // have to use the same version on all platforms.
  // Since 4.6.0 doesn't work on Linux, and 4.8.0 doesn't work with mathjax-node,
  // this probably won't work on any other platform :(
  //
  // For reasons that are not clear to me, if I declare this dependency as compileOnly (as I think it should be),
  // tests fail...
  implementation "com.eclipsesource.j2v8:j2v8_linux_x86_64:$j2v8Version"

  implementation gradleApi()
}

jar {
  manifest {
    attributes(
      'Implementation-Title'  : project.description,
      'Implementation-Version': project.version
    )
  }
}
